/*-------------------------------------------------------------------------*/

grammar CSV;

/*-------------------------------------------------------------------------*/

options {
	language = Java;
}

/*-------------------------------------------------------------------------*/
/* CSV PARSER                                                              */
/*-------------------------------------------------------------------------*/

file
	: (comment | row)*
	;

/*-------------------------------------------------------------------------*/

comment
	: COMMENT BREAK?
	;

/*-------------------------------------------------------------------------*/

row
	: field (',' field)* BREAK?
	;

/*-------------------------------------------------------------------------*/

field
	: STRING
	| VALUE
	;

/*-------------------------------------------------------------------------*/
/* CSV LEXER                                                               */
/*-------------------------------------------------------------------------*/

COMMENT
	: '#' ~[\n\r]*
	;

/*-------------------------------------------------------------------------*/

STRING
	: '"' (ESC | '""' | ~["\\])* '"'
	| '\'' (ESC | '\'\'' | ~['\\])* '\''
	;

VALUE
	: ~[,"'\n\r]+
	;

BREAK
	: '\r' '\n'
	| '\r'
	| '\n'
	;

/*-------------------------------------------------------------------------*/

WS
	: [ \t] + -> skip
	;

/*-------------------------------------------------------------------------*/

fragment ESC
	: '\\' (["'\\/bfnrt] | 'u' HEX HEX HEX HEX)
	;

fragment HEX
	: [a-fA-F0-9]
	;

/*-------------------------------------------------------------------------*/
