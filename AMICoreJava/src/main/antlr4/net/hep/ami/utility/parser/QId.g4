/*-------------------------------------------------------------------------*/

grammar QId;

/*-------------------------------------------------------------------------*/

options {
	language = Java;
}

/*-------------------------------------------------------------------------*/
/* QID PARSER                                                              */
/*-------------------------------------------------------------------------*/

qId
	: m_basicQId=basicQId ('{' m_constraintQIds+=constraintQId (',' m_constraintQIds+=constraintQId)* '}')?
	;

/*---------------------------*/

constraintQId
	: m_op='!'? m_qId=qId
	;

/*---------------------------*/

basicQId
	: m_ids+=ID ('.' m_ids+=ID)*
	;

/*-------------------------------------------------------------------------*/
/* QID LEXER                                                               */
/*-------------------------------------------------------------------------*/

ID
	: [a-zA-Z_][a-zA-Z0-9_#$]*
	| '`' ('``' | ~'`')+ '`'
	| '"' ('""' | ~'"')+ '"'
	| '*'
	| '#'
	;

/*-------------------------------------------------------------------------*/

WS
	: [ \t\n\r]+ -> skip
	;

/*-------------------------------------------------------------------------*/
