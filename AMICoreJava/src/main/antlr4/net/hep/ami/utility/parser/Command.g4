/*--------------------------------------------------------------------------------------------------------------------*/

grammar Command;

/*--------------------------------------------------------------------------------------------------------------------*/

options {
	language = Java;
}

@header {
	import java.util.*;

	import net.hep.ami.utility.*;
}

@members {

	private static class Pair
	{
		final String x;
		final String y;

		private Pair(String _x, String _y)
		{
			x = _x;
			y = _y;
		}
	}
}

/*--------------------------------------------------------------------------------------------------------------------*/
/* COMMAND PARSER                                                                                                     */
/*--------------------------------------------------------------------------------------------------------------------*/

command returns [ Command.CommandTuple commandTuple ]
	: identifier parameterList EOF { $commandTuple = new Command.CommandTuple($identifier.v, $parameterList.v); }
	;

/*--------------------------------------------------------------------------------------------------------------------*/

parameterList returns [ Map<String, String> v ]
	@init { $v = new LinkedHashMap<>(); }
	: (parameter { $v.put($parameter.v.x, $parameter.v.y); })*
	;

/*--------------------------------------------------------------------------------------------------------------------*/

parameter returns [ Pair v ]
	: '-'+ identifier '=' string { $v = new Pair($identifier.v, Utility.javaStringToText($string.v)); }
	| '-'+ identifier { $v = new Pair($identifier.v, ""); }
	;

/*--------------------------------------------------------------------------------------------------------------------*/

identifier returns [ String v ]
	: IDENTIFIER { $v = $IDENTIFIER.text; }
	;

/*--------------------------------------------------------------------------------------------------------------------*/

string returns [ String v ]
	: STRING { $v = $STRING.text; }
	;

/*--------------------------------------------------------------------------------------------------------------------*/
/* COMMAND LEXER                                                                                                      */
/*--------------------------------------------------------------------------------------------------------------------*/

IDENTIFIER
	: [a-zA-Z_][a-zA-Z0-9_]*
	;

STRING
	: '"' (ESC | ~["\\\r\n])* '"'
	| '\'' (ESC | ~['\\\r\n])* '\''
	;

/*--------------------------------------------------------------------------------------------------------------------*/

COMMENT
	: '#' ~[\n\r]* -> skip
	;

WS
	: [ \t\n\r]+ -> skip
	;

/*--------------------------------------------------------------------------------------------------------------------*/

fragment ESC
	: '\\' (["'/\\bfnrt] | 'u' HEX HEX HEX HEX)
	;

fragment HEX
	: [a-fA-F0-9]
	;

/*--------------------------------------------------------------------------------------------------------------------*/
